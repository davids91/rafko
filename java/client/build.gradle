buildscript {

    repositories {
        mavenLocal()
        mavenCentral()
    }

}

plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    // ASSUMES GRADLE 2.12 OR HIGHER. Use plugin version 0.7.5 with earlier gradle versions
    id 'com.google.protobuf' version '0.8.8'
    // id 'com.google.protobuf' version '0.7.5'
}


repositories{
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
    }
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    maven {
        url "https://mvnrepository.com/artifact/"
    }
    gradlePluginPortal()
}

javafx {
    modules = [ 'javafx.controls' , 'javafx.fxml', 'javafx.swing' ]
}

group 'org.rafko'
version '1.0-SNAPSHOT'

sourceSets {
    main {
        proto {
            srcDirs '../../proto' /* include proto files from base project level */
        }
    }
}

sourceCompatibility = 1.11

def grpcVersion = '1.30.1'
def protobufVersion = '3.12.0'
def protocVersion = protobufVersion

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"

    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:2.28.2"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

application {
    mainClassName = 'control.DashboardEntryPoint'
}
